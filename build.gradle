/*
 * Copyright 2015 Oleg Khalidov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

subprojects {
    def GROUP_ID = gradle.ext.GROUP_ID
    def ARTIFACT_ID = project.name
    def VERSION = gradle.ext.VERSION
    def VERSION_DESC = gradle.ext.VERSION_DESC
    def VERSION_VCS_TAG = VERSION

    group = gradle.ext.GROUP_ID
    version = gradle.ext.VERSION

    apply plugin: 'eclipse'

    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = JavaVersion.VERSION_1_5
    targetCompatibility = JavaVersion.VERSION_1_7

    publishing {
        publications {
            pubs(MavenPublication) {
                from components.java
                if (tasks.findByName('sourcesJar'))
                    artifact sourcesJar
                if (tasks.findByName('noAptJar'))
                    artifact noAptJar
            }
        }
    }

    bintray {
        user = 'brooth'
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('bintrayApiKey')
        publications = ['pubs']

        pkg {
            repo = 'maven'
            name = "$GROUP_ID:$ARTIFACT_ID"
            userOrg = 'brooth'

            version {
                name = VERSION
                desc = VERSION_DESC
                vcsTag = VERSION_VCS_TAG
            }
        }
    }
}
