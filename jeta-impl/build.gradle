/*
 * Copyright 2015 Oleg Khalidov
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def GROUP_ID = 'org.brooth.jeta'
def ARTIFACT_ID = 'jeta'
def VERSION = '0.1-alpha'
def VERSION_DESC = 'First alpha version'
def VERSION_VCS_TAG = VERSION

group = GROUP_ID
version = VERSION

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.4'
    }
}

apply plugin: 'java'
apply plugin: 'net.ltgt.apt'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile project(':jeta-api')
    compile 'com.google.guava:guava:18.0'

    testApt project(':jeta-apt-impl')
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'junit', name: 'junit-dep', version: '4.10'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.+'
}

tasks.withType(JavaCompile) { task ->
    def args = task.options.compilerArgs
    args << '-AjetaDebug=true'
    args << '-AjetaMapMetasitoryPackage=org.brooth.jeta'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId GROUP_ID
            artifactId ARTIFACT_ID
            version VERSION
        }
    }
}

bintray {
    user = 'brooth'
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv("BINTRAY_API_KEY")
    publications = ['MyPublication']

    pkg {
        repo = 'maven'
        name = "$GROUP_ID:$ARTIFACT_ID"
        userOrg = 'brooth'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/bintray/gradle-bintray-plugin'
        issueTrackerUrl = 'https://github.com/brooth/jeta/issues'
        vcsUrl = 'https://github.com/brooth/jeta.git'

        version {
            name = VERSION
            desc = VERSION_DESC
            vcsTag = VERSION_VCS_TAG
        }
    }
}
